# ------------------------------------------------------------
# Target: mlio-protobuf
# ------------------------------------------------------------

protobuf_generate_cpp(
    #SRCS
        _PROTOBUF_CPP_SRCS
    #HDRS
        _PROTOBUF_CPP_HDRS
    #PROTOS
        recordio_protobuf.proto
)

add_library(mlio-protobuf OBJECT
    ${_PROTOBUF_CPP_SRCS}
)

# We treat protoc-generated header files as system files to suppress
# non-actionable warnings.
target_include_directories(mlio-protobuf SYSTEM
    PUBLIC
        $<BUILD_INTERFACE:${PROJECT_BINARY_DIR}/src>
)

set_target_properties(mlio-protobuf PROPERTIES
    CXX_CLANG_TIDY
        ""
    CXX_INCLUDE_WHAT_YOU_USE
        ""
)

# Source files generated by the old versions of protoc cause lots of
# non-actionable warnings.
target_compile_options(mlio-protobuf
    PRIVATE
        -w
)

target_link_libraries(mlio-protobuf
    PUBLIC
        protobuf::libprotobuf
)

# Building a static library exposes its private dependencies in the
# CMake targets file; therefore we have to make sure that
# mlio-protobuf can be found by adding it to the same CMake targets
# file. We do not need to specify its artifacts though as they are
# already part of the library.
install(
    TARGETS
        mlio-protobuf
    EXPORT
        mlio-targets
)
